package fr.utbm.ia54.acotsp

/** 
 * 
 */
import io.sarl.core.Destroy
import io.sarl.core.Initialize
import io.sarl.core.Logging
import java.util.UUID
import java.util.ArrayList
import io.sarl.core.DefaultContextInteractions
import io.sarl.core.Lifecycle

/** 
 * @author Omen
 * 
 */
@SuppressWarnings("potential_field_synchronization_problem")
agent ACOAgent {
	uses Logging, DefaultContextInteractions, Lifecycle

	uses ProbabilitiesComputation

	var environment : UUID

	var startingCity : Integer

	var acoParameters : ACOParameters

	var pheromones : Double[][]

	var currentCity : Integer

	var currentPathLength : Integer 

	var visitedCities : ArrayList<Integer>

	var visitedClusters : ArrayList<Integer>

	on Initialize {
		if (occurrence.parameters.size > 8) {
			if (occurrence.parameters.get(0) instanceof UUID) {
				environment = occurrence.parameters.get(0) as UUID
			}

			if (occurrence.parameters.get(1) instanceof Integer) {
				startingCity = occurrence.parameters.get(1) as Integer
			}

			if (occurrence.parameters.get(2) instanceof ACOParameters) {
				acoParameters = occurrence.parameters.get(2) as ACOParameters
			}
		}
		setSkill(
			new ProbabilitiesComputationWithGroupInfluenceSkill(acoParameters.numberOfCities, acoParameters.distances,
				acoParameters.attachedCluster, acoParameters.pheromoneRegulationFactor,
				acoParameters.visibilityRegulationFactor), ProbabilitiesComputation)
		info("The agent was started.")
	}

	on NewIteration {
		pheromones = occurrence.pheromones
		buildPath()
		emit(new IterationFinished(visitedCities, currentPathLength))[it.UUID == environment]
	}
	
	on OptimizationFinished {
		killMe
	}

	def buildPath() : void {
		visitedCities = new ArrayList<Integer>
		visitedClusters = new ArrayList<Integer>
		currentPathLength = 0
		currentCity = startingCity
		visitedCities.add(startingCity)
		visitedClusters.add(acoParameters.attachedCluster.get(startingCity))
		while (visitedClusters.size() < acoParameters.numberOfClusters) {
			var probabilities = new ArrayList<Float>
			probabilities = probabilitiesComputation(currentCity, probabilities, visitedCities, visitedClusters,
				pheromones)
			var nextVisitedCity = probabilities.indexOf(probabilities.max())
			currentPathLength = currentPathLength + acoParameters.distances.get(currentCity).get(nextVisitedCity)
			currentCity = nextVisitedCity
			visitedCities.add(nextVisitedCity)
			visitedClusters.add(acoParameters.attachedCluster.get(nextVisitedCity))
		}
		currentPathLength = currentPathLength + acoParameters.distances.get(currentCity).get(startingCity)
	}

	on Destroy {
		// Event trigger when the agent is destroyed from the system.
		// You should put all the resource releasing statements in this block of code.
		info("The agent was stopped.")
	}

}
